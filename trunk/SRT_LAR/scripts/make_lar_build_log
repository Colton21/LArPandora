#!/bin/csh

if ( $#argv != 1 ) then
	echo usage: make_lar_build_log input_log
	exit 1
endif

set loginp = $1
ls $loginp >& /dev/null
if ($?) then
	echo input file $loginp not found
	exit 1
endif

set webdir = "/nusoft/app/web/htdoc/larsoft/build_logs"
set day = `date +%a`
set logfile = larsoft_fnal_build_$day.html
set logoutp = "$webdir/$logfile"
set tmpname = "larsoft_tmp.html"

# make a temporary copy that's more web friendly

sed -e 's#$#<br>#g' $loginp >! $tmpname

# build the final log file, including highlighting errors and conflicts

echo "<html>" >! $logoutp
echo "<body>" >> $logoutp
echo '<p style="color:red">' >> $logoutp
echo " <br>">>$logoutp
echo "--------------------------------------------- <br>">>$logoutp
echo "Any Build Errors and Repository Conflicts follow. <br>">>$logoutp
echo "(See the body of the logfile for context.) <br>">>$logoutp
grep -i 'error:' $tmpname >> $logoutp
grep -i conflict $tmpname >> $logoutp
echo " <br>">>$logoutp
echo "Done with errors and conflicts <br>">>$logoutp
echo "--------------------------------------------- <br>">>$logoutp
echo "</p>" >> $logoutp

echo '<p style="color:orange">' >> $logoutp
echo " <br>">>$logoutp
echo "--------------------------------------------- <br>">>$logoutp
echo "Any Build Warnings follow. <br>">>$logoutp
echo "(See the body of the logfile for context.) <br>">>$logoutp
grep 'warning: ' $tmpname >> $logoutp
echo " <br>">>$logoutp
echo "Done with warnings <br>">>$logoutp
echo "--------------------------------------------- <br>">>$logoutp
echo "</p>" >> $logoutp

cat $tmpname >> $logoutp
echo "</body>" >> $logoutp
echo "</html>" >> $logoutp

rm -f $tmpname

# make a link to the latest log file
set target = "$webdir/latest_build.html"
rm -f $target
cp $logoutp $target
